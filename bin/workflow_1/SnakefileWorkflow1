# Define the variable from the configuration file
configfile: "configuration_w1.yaml"
GROUPS = ["life_expectancy", "gdp_per_capita", "median_age", "population_density"]

# Rule to specify the expected output files for the chosen variable
rule all:
    input:
        '../../data/owid-covid-data_processed.csv',
        expand('../../data/owid-covid-data_processed_{group}.csv', group=GROUPS),
        expand('../../results/workflow_1/mannwhitneyu_{group}.txt', group=GROUPS),
        expand('../../results/workflow_1/plot_{group}.png', group=GROUPS)

rule clean:
    shell: 'rm -f ../../results/workflow_1/*.png ../../results/workflow_1/*.txt'

rule dataprocessing:
    input:
        cmd = '../dataprocessing.py',
        csv = '../../data/owid-covid-data.csv'
    output: '../../data/owid-covid-data_processed.csv'
    shell: 'python {input.cmd} -i {input.csv} -o {output}'

rule dataprocessingw1:
    input:
        cmd = 'dataprocessing_w1.py',
        csv = '../../data/owid-covid-data_processed.csv'
    output: '../../data/owid-covid-data_processed_{group}.csv'
    params:
        continent = config['continent'],
        year = config['year'],
    shell: 'python {input.cmd} -i {input.csv} -c "{wildcards.group}" -o {output} -y {params.year} --continent "{params.continent}"'

rule mannwhitneyu:
    input:
        cmd = 'mann_whitney_u_w1.py',
        csv = '../../data/owid-covid-data_processed_{group}.csv'
    output: '../../results/workflow_1/mannwhitneyu_{group}.txt'
    params:
        y = config['y']
    shell: 'python {input.cmd} {input.csv} -x "{wildcards.group}_cat" -y "{params.y}" -o {output}'

rule plot:
    input:
        mannwhitneyu_test = '../../results/workflow_1/mannwhitneyu_{group}.txt',
        csv = '../../data/owid-covid-data_processed_{group}.csv'
    output:
        plot = '../../results/workflow_1/plot_{group}.png'
    params:
        y = config['y']
    run:
        with open(input.mannwhitneyu_test, 'r') as f:
            result = f.read().strip()
        if result == 'True':
            shell('python box_plot_w1.py {input.csv} -g "{wildcards.group}_cat" -y "{params.y}" -o {output.plot}')
        else:
            shell('python line_plot_w1.py {input.csv} -g "{wildcards.group}_cat" -x "month" -y "{params.y}" -o {output.plot}')
